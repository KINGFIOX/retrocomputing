{-# LANGUAGE ApplicativeDo #-}
{-# LANGUAGE DuplicateRecordFields #-}
{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE NumericUnderscores #-}
{-# LANGUAGE PartialTypeSignatures #-}
{-# LANGUAGE RecordWildCards #-}
{-# LANGUAGE ViewPatterns #-}
{-# OPTIONS_GHC -Wno-incomplete-patterns #-}
{-# HLINT ignore "Functor law" #-}
{-# OPTIONS_GHC -Wno-missing-signatures #-}
{-# OPTIONS_GHC -Wno-orphans #-}
{-# OPTIONS_GHC -Wno-partial-type-signatures #-}
{-# OPTIONS_GHC -Wno-unrecognised-pragmas #-}
{-# OPTIONS_GHC -Wno-unused-foralls #-}

-- {-# LANGUAGE AllowAmbiguousTypes #-}

module Chap8_4_2.Project where

import Clash.Prelude
import Data.Maybe (isJust, isNothing)
import Simple

-- å°æ¢…å“¥ fpga vga https://www.bilibili.com/video/BV1izCYYqEoY

-- vgaDriver :: (HiddenClockResetEnable dom, KnownNat w, KnownNat h) => VGATimings w h -> VGADriver dom w h
-- vga640x480at60 :: VGATimings 640 480
-- vga640x480at75 :: VGATimings 640 480

-- rust
-- enum VGAState<visbile, front, pulse, back> {
--   Visible(Index<visual>),
--   Visible(Index<visual>),
-- }

data VGAState visible front pulse back
  = Visible (Index visible)
  | FrontPorch (Index front)
  | SyncPulse (Index pulse)
  | BackPorch (Index back)
  deriving (Show, Generic, NFDataX)

data VGATiming (visible :: Nat) = forall front pulse back.
  VGATiming
  { polarity :: Polarity, -- è¿™é‡Œ, ä» type-level å˜æˆäº† term-level
    preWidth :: SNat front,
    pulseWidth :: SNat pulse, -- SNat<pulse>
    postWidth :: SNat back
  }

deriving instance Show (VGATiming vis)

data VGATimings (ps :: Nat) (w :: Nat) (h :: Nat) = VGATimings
  { vgaHorizTiming :: VGATiming w,
    vgaVertTiming :: VGATiming h
  }
  deriving (Show)

visible :: VGAState visible front pulse back -> Maybe (Index visible)
visible (Visible coord) = Just coord
visible _ = Nothing

sync :: VGAState visible front pulse back -> Bool
sync SyncPulse {} = True
sync _ = False

end :: (KnownNat back) => VGAState visible front pulse back -> Bool
end (BackPorch cnt) | cnt == maxBound = True
end _ = False

type Step a = a -> a

-- using FuncPtr = VGAState<visible, front, pulse, back> (*)(VGAState<visible, front, pulse, back>);
-- struct VGACounter<visible> (FuncPtr)

data VGACounter visible
  = forall front pulse back.
    (KnownNat front, KnownNat pulse, KnownNat back) =>
    VGACounter (Step (VGAState visible front pulse back))

mkVGACounter ::
  SNat front ->
  SNat pulse ->
  SNat back ->
  Step (VGAState visible front pulse back) ->
  VGACounter visible
mkVGACounter SNat SNat SNat = VGACounter

vgaCounter ::
  (KnownNat visible) => VGATiming visible -> VGACounter visible
vgaCounter (VGATiming _ front@SNat pulse@SNat back@SNat) =
  mkVGACounter front pulse back $ \case
    Visible cnt -> count Visible FrontPorch cnt
    FrontPorch cnt -> count FrontPorch SyncPulse cnt
    SyncPulse cnt -> count SyncPulse BackPorch cnt
    BackPorch cnt -> count BackPorch Visible cnt
  where
    -- { x | x \in Index<n> } <=> { x | 0 <= x < n }
    -- unsigned int, gcc linux x64, 2^32 - 1
    count :: (KnownNat n, KnownNat m) => (Index n -> a) -> (Index m -> a) -> Index n -> a
    count this next = maybe (next 0) this . succIdx

data VGASync dom = VGASync
  { vgaHSync :: "HSYNC" ::: Signal dom Bit,
    vgaVSync :: "VSYNC" ::: Signal dom Bit,
    vgaDE :: "DE" ::: Signal dom Bool -- display enable
  }

data VGAOut dom r g b = VGAOut
  { vgaSync :: "SYNC" ::: VGASync dom,
    vgaR :: "RED" ::: Signal dom (Unsigned r),
    vgaG :: "GREEN" ::: Signal dom (Unsigned g),
    vgaB :: "BLUE" ::: Signal dom (Unsigned b)
  }

data VGADriver dom w h = VGADriver
  { vgaSync :: VGASync dom, -- é‡åå­—æ®µ, æ³¨æ„, DuplicateRecordFields
    vgaX :: Signal dom (Maybe (Index w)),
    vgaY :: Signal dom (Maybe (Index h))
  }

vgaDriver ::
  (HiddenClockResetEnable dom, KnownNat w, KnownNat h) =>
  (DomainPeriod dom ~ ps) =>
  VGATimings ps w h ->
  VGADriver dom w h
vgaDriver VGATimings {..} =
  case (vgaCounter vgaHorizTiming, vgaCounter vgaVertTiming) of
    (VGACounter nextH, VGACounter nextV) -> VGADriver {..}
      where
        stateH = register (Visible 0) $ nextH <$> stateH
        stateV = regEn (Visible 0) endLine $ nextV <$> stateV
        endLine = end <$> stateH
        vgaX = visible <$> stateH
        vgaY = visible <$> stateV
        vgaSync =
          VGASync
            { vgaDE = isJust <$> vgaX .&&. isJust <$> vgaY,
              vgaVSync = toActiveDyn (polarity vgaVertTiming) . sync <$> stateV,
              vgaHSync = toActiveDyn (polarity vgaHorizTiming) . sync <$> stateH
            }

vgaOut ::
  (HiddenClockResetEnable dom, KnownNat r, KnownNat g, KnownNat b) =>
  VGASync dom ->
  Signal dom (Unsigned r, Unsigned g, Unsigned b) ->
  VGAOut dom r g b
vgaOut vgaSync@VGASync {..} rgb = VGAOut {..}
  where
    (vgaR, vgaG, vgaB) = unbundle $ blank rgb
    blank = mux (not <$> vgaDE) $ pure (0, 0, 0)

createDomain vSystem {vName = "Dom25", vPeriod = hzToPeriod 25_175_000}

rgbwBars ::
  (KnownNat w, KnownNat h, KnownNat r, KnownNat g, KnownNat b) =>
  (HiddenClockResetEnable dom) =>
  Signal dom (Maybe (Index w)) ->
  Signal dom (Maybe (Index h)) ->
  Signal dom (Unsigned r, Unsigned g, Unsigned b)
rgbwBars x _ = colors !!. counter
  where
    counter = register (0 :: Index 3) $ mux (isNothing <$> x) (pure 0) $ nextIdx <$> counter
    colors = red :> green :> blue :> Nil
      where
        red = (maxBound, 0, 0)
        green = (0, maxBound, 0)
        blue = (0, 0, maxBound)

topEntity ::
  "CLK_25MHZ" ::: Clock Dom25 ->
  "RESET" ::: Reset Dom25 ->
  "VGA" ::: VGAOut Dom25 8 8 8
topEntity = withEnableGen board
  where
    -- There is no type annotation in the book. However, without this, the code would analyzed with error ğŸ›
    board :: (HiddenClockResetEnable Dom25) => VGAOut Dom25 8 8 8
    board = vgaOut vgaSync rgb
      where
        VGADriver {..} = vgaDriver vga640x480at60
        rgb = bouncingBall vgaX vgaY

vga640x480at60 :: VGATimings (HzToPeriod 25_175_000) 640 480
vga640x480at60 =
  VGATimings
    { vgaHorizTiming = VGATiming Low (SNat @16) (SNat @96) (SNat @48),
      vgaVertTiming = VGATiming Low (SNat @11) (SNat @2) (SNat @31)
    }

type BallSize = 35

-- è¿™æ˜¯ä¸€ä¸ªå¼¹è·³å°çƒçš„åŠ¨ç”»ç”Ÿæˆå™¨
bouncingBall ::
  (KnownNat w, KnownNat h, KnownNat r, KnownNat g, KnownNat b) =>
  ((BallSize + 2) <= w, (BallSize + 1) <= h) => -- çº¦æŸ w å’Œ h è‡³å°‘æ˜¯ BallSize + 2, BallSize + 1
  (HiddenClockResetEnable dom) =>
  Signal dom (Maybe (Index w)) ->
  Signal dom (Maybe (Index h)) ->
  Signal dom (Unsigned r, Unsigned g, Unsigned b)
bouncingBall vgaX vgaY = draw
  where
    draw = mux isBall ballColor backColor -- (vgaX, vgaY) æ˜¯å¦åœ¨å°çƒçš„èŒƒå›´
    isBall = (near <$> ballX <*> vgaX) .&&. (near <$> ballY <*> vgaY)
      where
        near x0 = maybe False $ \(fromIntegral -> x) -> x0 <= x && x < (x0 + ballSize)

    ballColor = pure (0xf0, 0xe0, 0x40) -- Yellow
    backColor = pure (0x30, 0x30, 0x30) -- Dark gray
    ballSize :: (Num a) => a
    ballSize = snatToNum (SNat @BallSize)

    rightWall = maxOf vgaX - ballSize
    bottomWall = maxOf vgaY - ballSize

    maxOf :: forall n p. (KnownNat n, 1 <= n) => p (Maybe (Index n)) -> Signed (CLog 2 n + 1)
    maxOf _ = fromIntegral (maxBound :: Index n)

    frameEnd = isFalling False (isJust <$> vgaY)

    (ballX, speedX) = unbundle $ regEn (0, 2) frameEnd $ bounceBetween (0, rightWall) <$> bundle (ballX, speedX)
    (ballY, speedY) = unbundle $ regEn (0, 1) frameEnd $ bounceBetween (0, bottomWall) <$> bundle (ballY, speedY)

move :: (Num a) => (a, a) -> (a, a)
move (x, dx) = (x + dx, dx)

reflect :: (Num a, Num a', Ord a, Ord a') => (a, a') -> (a, a') -> (a, a')
reflect (p, n) (x, dx)
  | sameDirection n dist = (p + dist, negate dx)
  | otherwise = (x, dx)
  where
    -- å¦‚æœç¢°åˆ°äº†ä¸Šè¾¹ç•Œ, p - x å°±æ˜¯ -, ç„¶å -1 ä¹Ÿæ˜¯ -, åŒå·
    -- å¦‚æœæ²¡ç¢°åˆ°ä¸Šè¾¹ç•Œ, p - x å°±æ˜¯ +, ç„¶å -1 æ˜¯ -, å¼‚å·
    sameDirection u v = compare 0 u == compare 0 v
    dist = p - x -- è¾¹ç•Œä¸å°çƒçš„è·ç¦»

-- ç¬¬ä¸€ä¸ªå‚æ•°: è¾¹ç•Œ
bounceBetween :: (Num a, Ord a) => (a, a) -> (a, a) -> (a, a)
bounceBetween (lo, hi) = reflect (lo, 1) . reflect (hi, -1) . move -- ç¢°åˆ°äº†ä¸Šç•Œ, å¾€å›èµ° -1; ç¢°åˆ°äº†ä¸‹ç•Œ, å¾€æ­£æ–¹å‘èµ°

scalableRGBBars1 x _ = colors !!. counter
  where
    newColumn = changed Nothing x -- x0, åˆå€¼ä¸º Nothing
    counter = regEn (0 :: Index 3) newColumn $ mux (isNothing <$> x) (pure 0) (nextIdx <$> counter) -- å½©æ¡
    colors = red :> green :> blue :> Nil
      where
        red = (maxBound, 0, 0)
        green = (0, maxBound, 0)
        blue = (0, 0, maxBound)

scalableRGBBars x _ = colors !!. counterNext -- å‰é€’äº†, ä¸Šé¢é‚£ç§æ˜¯å»¶è¿Ÿäº†ä¸€ä¸ªå‘¨æœŸ
  where
    newColumn = changed Nothing x
    counter = register (0 :: Index 3) counterNext
    counterNext =
      mux (not <$> newColumn) counter $
        mux (isNothing <$> x) (pure maxBound) $
          nextIdx <$> counter
    colors = red :> green :> blue :> Nil
      where
        red = (maxBound, 0, 0)
        green = (0, maxBound, 0)
        blue = (0, 0, maxBound)

-- | @maskSides@
-- å¯¹ VGA ä¹‹ç±»çš„ "å…ˆè·‘åæ ‡åœ¨ç”»åƒç´ " çš„ç”µè·¯æ¥è¯´, mask å¾ˆæœ‰ç”¨
-- æŠŠæ•´è¡Œé‡Œå‰ k ä¸ªåƒç´ å’Œå m ä¸ªåƒç´ éƒ½å±è”½æ‰, åªç•™ä¸‹ä¸­é—´çš„ n ä¸ªåƒç´ 
-- @k@ å±è”½æ‰å‰é¢ k ä¸ªåƒç´ 
-- @raw@ ä¸€æ¡ä¿¡å·çº¿
--   - Nothing å½“å‰åœ¨ä¸å¯è§åŒº
--   - Just i æ‰«æåˆ°çš„ä½ç½® [0 .. k + n + m - 1]
-- @return@ ä¸€æ¡ä¿¡å·çº¿
--   - Nothing ä¸åœ¨ "æ„Ÿå…´è¶£çš„ä¸­é—´æ®µ"
--   - Just i åœ¨ "æ„Ÿå…´è¶£çš„ä¸­é—´æ®µ", [0 .. n - 1]
maskSides ::
  (KnownNat n, KnownNat m, KnownNat k) =>
  (HiddenClockResetEnable dom) =>
  SNat k ->
  Signal dom (Maybe (Index (k + n + m))) ->
  Signal dom (Maybe (Index n))
maskSides k raw = transformed
  where
    changed_ = register Nothing raw ./=. raw
    started = raw .== Just (snatToNum k) -- è¯´æ˜æ‰«ææªå·²ç»æ‰«æåˆ°ç¬¬ k ä¸ªåƒç´ äº†, å¼€å§‹æŠ€æœ¯
    r = register Nothing transformed -- ä¿å­˜ç»“æœ
    transformed =
      mux (not <$> changed_) r $ -- å¦‚æœåæ ‡ä¸å˜, æ²¿ç”¨ä¸Šä¸€æ‹çš„ç»“æœ
        mux (isNothing <$> raw) (pure Nothing) $ -- å¦åˆ™, å¦‚æœ raw æ˜¯ Nothing, è¯´æ˜æ‰«ææªå·²ç»æ‰«æåˆ°ä¸å¯è§åŒºäº†, è¿”å› Nothing
          mux started (pure $ Just 0) $ -- å¦‚æœæ‰«æåˆ°äº†ç¬¬ k ä¸ªåƒç´ , å¼€å§‹è®¡æ•°
            (succIdx =<<) <$> r

maskStart ::
  forall k n dom.
  (KnownNat k, KnownNat n) =>
  (HiddenClockResetEnable dom) =>
  Signal dom (Maybe (Index (k + n))) ->
  Signal dom (Maybe (Index n))
maskStart = maskSides (SNat @k) -- å±è”½å‰ k ä¸ªåƒç´ 

maskEnd ::
  forall k n dom.
  (KnownNat n, KnownNat k) =>
  (HiddenClockResetEnable dom) =>
  Signal dom (Maybe (Index (n + k))) ->
  Signal dom (Maybe (Index n))
maskEnd = maskSides (SNat @0) -- å‰é¢ä¸å±è”½, æ€»é•¿æ˜¯ n+k, æ„Ÿå…´è¶£çš„æ®µæ˜¯ n ä¸ªåƒç´ , åé¢ k ä¸ªåƒç´ å°±ä¸å†æ˜¾ç¤ºäº†, å…¶å®ä¹Ÿå°±æ˜¯è¢«å±è”½äº†

center ::
  forall n n0 k m dom.
  (KnownNat n, KnownNat n0, KnownNat k, KnownNat m) =>
  (k ~ ((n0 - n) `Div` 2), n0 ~ (k + n + m)) => -- k = \floor ((n0 - n) / 2), åŸæ¥çš„é•¿åº¦æ˜¯ n0. å±…ä¸­, k: å·¦è¾¹è¦å±è”½æ‰å¤šå°‘
  (HiddenClockResetEnable dom) =>
  Signal dom (Maybe (Index n0)) ->
  Signal dom (Maybe (Index n))
center = maskSides (SNat @k)

-- | @scale@
-- æ”¾ç¼©ä¿¡å·, æŠŠ n*k ä¸ªåƒç´ æ”¾ç¼©ä¸º n ä¸ªåƒç´ 
-- @k@ æ”¾ç¼©å› å­, k ä¸ªåƒç´ ä¸ºä¸€ç»„, ç„¶åä¸€ç»„é‡Œé¢å–ä¸€ä¸ªä»£è¡¨åƒç´ 
-- @raw@ åŸå§‹ä¿¡å·ä¸º n*k ä¸ªåƒç´ , æ”¾ç¼©åä¸º n ä¸ªåƒç´ 
-- @return@ ç»„ç¼–å·ã€ç»„å†…çš„åƒç´ ç¼–å·
scale ::
  forall n k dom.
  (KnownNat n, KnownNat k, 1 <= k) =>
  (HiddenClockResetEnable dom) =>
  SNat k -> -- æ”¾ç¼©å› å­, k ä¸ªåƒç´ ä¸ºä¸€ç»„, ç„¶åä¸€ç»„é‡Œé¢å–ä¸€ä¸ªä»£è¡¨åƒç´ 
  Signal dom (Maybe (Index ((*) n k))) -> -- åŸå§‹ä¿¡å·ä¸º n*k ä¸ªåƒç´ , æ”¾ç¼©åä¸º n ä¸ªåƒç´ 
  (Signal dom (Maybe (Index n)), Signal dom (Maybe (Index k)))
scale _ raw = (scaledNext, enable (isJust <$> scaledNext) counterNext)
  where
    prev = register Nothing raw -- ä¸Šä¸€æ‹ (åƒç´ æ—¶é’Ÿ)
    changed_ = raw ./=. prev -- æ˜¯å¦å˜åŒ–

    counter = register 0 counterNext -- è®¡æ•°å™¨
    counterNext =
      mux (not <$> changed_) counter $ -- å¦‚æœæ²¡æœ‰å‘ç”Ÿå˜åŒ–, è®¡æ•°å™¨ä¸å˜
        mux (isNothing <$> prev) (pure 0) $ -- å¦‚æœä¸Šä¸€æ‹æ˜¯ Nothing, è¯´æ˜æ‰«ææªå·²ç»æ‰«æåˆ°ä¸å¯è§åŒºäº†, è®¡æ•°å™¨æ¸…é›¶
          nextIdx <$> counter -- å¦åˆ™, è®¡æ•°å™¨è‡ªå¢

    scaled = register Nothing scaledNext -- ç»„ç¼–å·
    scaledNext =
      mux (not <$> changed_) scaled $ -- å¦‚æœæ²¡æœ‰å‘ç”Ÿå˜åŒ–, æ”¾ç¼©åçš„ä¿¡å·ä¸å˜
        mux (counterNext .== 0) (maybe (Just 0) succIdx <$> scaled) $ -- åªæœ‰è®¡æ•°å™¨å›åˆ° 0 æ—¶ scaled++
          scaled
